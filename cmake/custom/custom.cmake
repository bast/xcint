# FIXME
option(ENABLE_FORTRAN_INTERFACE "Build Fortran interface" OFF)
if(ENABLE_FORTRAN_INTERFACE)
    enable_language(C CXX Fortran)
else()
    enable_language(C CXX)
endif()

# FIXME
option(USE_EXTERNAL_NUM_GRID "Use external numerical grid" OFF)
if(USE_EXTERNAL_NUM_GRID)
    add_definitions(-DUSE_EXTERNAL_NUM_GRID)
endif()

# XCFun code
if(MPI_FOUND)
    set(PARENT_DEFINITIONS "${PARENT_DEFINITIONS} -DENABLE_MPI")
endif()
set(ExternalProjectCMakeArgs
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=${PROJECT_BINARY_DIR}/external
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DENABLE_FORTRAN_INTERFACE=OFF
    )
add_external(xcfun)

# numgrid code
set(ExternalProjectCMakeArgs
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX=${PROJECT_BINARY_DIR}/external
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DENABLE_MPI=${ENABLE_MPI}
    )
add_external(numgrid)
include_directories(${PROJECT_SOURCE_DIR}/external/numgrid/api)

include_directories(
    ${PROJECT_SOURCE_DIR}/src
    ${PROJECT_SOURCE_DIR}/src/density
    ${PROJECT_BINARY_DIR}/external/include
    ${PROJECT_SOURCE_DIR}/external/lebedev
    ${PROJECT_SOURCE_DIR}/external/googletest
    ${PROJECT_SOURCE_DIR}/external/googletest/include
    ${PROJECT_SOURCE_DIR}/api
    ${PROJECT_BINARY_DIR}/generated
    )

file(MAKE_DIRECTORY ${PROJECT_BINARY_DIR}/generated)

# generate AO evaluation code
add_custom_command(
    OUTPUT
        ${PROJECT_BINARY_DIR}/generated/autogenerated.h
        ${PROJECT_BINARY_DIR}/generated/autogenerated_0.cpp
        ${PROJECT_BINARY_DIR}/generated/autogenerated_1.cpp
        ${PROJECT_BINARY_DIR}/generated/autogenerated_2.cpp
        ${PROJECT_BINARY_DIR}/generated/autogenerated_3.cpp
        ${PROJECT_BINARY_DIR}/generated/autogenerated_4.cpp
        ${PROJECT_BINARY_DIR}/generated/autogenerated_5.cpp
        ${PROJECT_BINARY_DIR}/generated/parameters.h
        ${PROJECT_BINARY_DIR}/generated/offsets.h
    COMMAND
        ${PYTHON_EXECUTABLE} ${PROJECT_SOURCE_DIR}/src/density/generate.py ${PROJECT_BINARY_DIR}/generated
    WORKING_DIRECTORY
        ${PROJECT_SOURCE_DIR}/src/density
    DEPENDS
        src/density/generate.py
        src/density/cs_trans.py
    )


add_library(
    xcint
    src/io.cpp
    src/rolex.cpp
    src/Functional.cpp
    src/XCint.cpp
    src/MemAllocator.cpp
    api/xcint_c_api.cpp
    src/density/ao_vector.cpp
    src/density/Basis.cpp
    src/density/AOBatch.cpp
    ${PROJECT_BINARY_DIR}/generated/autogenerated_0.cpp
    ${PROJECT_BINARY_DIR}/generated/autogenerated_1.cpp
    ${PROJECT_BINARY_DIR}/generated/autogenerated_2.cpp
    ${PROJECT_BINARY_DIR}/generated/autogenerated_3.cpp
    ${PROJECT_BINARY_DIR}/generated/autogenerated_4.cpp
    ${PROJECT_BINARY_DIR}/generated/autogenerated_5.cpp
    )

set(BUILDNAME
    "${CMAKE_SYSTEM_NAME}-${CMAKE_HOST_SYSTEM_PROCESSOR}-${CMAKE_Fortran_COMPILER_ID}-${BLAS_TYPE}-${CMAKE_BUILD_TYPE}"
    CACHE STRING
    "Name of build on the dashboard"
    )

set(DART_TESTING_TIMEOUT
    "1200"
    CACHE STRING
    "Set timeout in seconds for every single test"
    )

include(CTest)
enable_testing()

foreach(_test energy_lda energy_b3lyp)
    add_executable(test_${_test} test/${_test}.cpp)

    target_link_libraries(
        test_${_test}
        xcint
        ${PROJECT_BINARY_DIR}/external/xcfun-build/libxcfun.a
        ${PROJECT_BINARY_DIR}/external/numgrid-build/libnumgrid.a
        ${MATH_LIBS}
        )

    if(MPI_FOUND)
        add_test(${_test}_mpi mpirun -np 4 ${PROJECT_BINARY_DIR}/test_${_test})
    else()
        add_test(${_test} ${PROJECT_BINARY_DIR}/test_${_test})
    endif()
endforeach()

# generate interface headers
add_custom_command(
    OUTPUT
        ${PROJECT_BINARY_DIR}/generated/xcint_c_parameters.h
        ${PROJECT_BINARY_DIR}/generated/xcint_fortran_parameters.h
    COMMAND
        ${PYTHON_EXECUTABLE} ${PROJECT_SOURCE_DIR}/api/generate.py ${PROJECT_BINARY_DIR}/generated/xcint_c_parameters.h ${PROJECT_BINARY_DIR}/generated/xcint_fortran_parameters.h
    WORKING_DIRECTORY
        ${PROJECT_SOURCE_DIR}
    DEPENDS
        ${PROJECT_SOURCE_DIR}/api/generate.py
    )
add_custom_target(
    generate_interface_parameters
    ALL
    DEPENDS
        ${PROJECT_BINARY_DIR}/generated/xcint_c_parameters.h
        ${PROJECT_BINARY_DIR}/generated/xcint_fortran_parameters.h
    )


add_dependencies(xcint xcfun)
add_dependencies(xcint numgrid)
add_dependencies(xcint generate_interface_parameters)

if(ENABLE_FORTRAN_INTERFACE)
    add_library(
        xcint_fortran_api
        api/xcint_fortran_api.F90
        )
endif()

install(TARGETS xcint ARCHIVE DESTINATION lib)
